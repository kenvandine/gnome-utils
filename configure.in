
AC_INIT(gtt)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-utils, 0.13)

AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT

AC_ISC_POSIX
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AM_PROG_LEX
AC_PROG_YACC
dnl AC_DECL_YYTEXT
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

GNOME_X_CHECKS

GNOME_CHECK_OBJC
if test $OBJECTIVE_C = yes; then
  GNOME_INIT_OBJC
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_ST_RDEV
AC_HEADER_TIME
AC_STRUCT_TM
AC_TYPE_UID_T

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_MMAP
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(getcwd gettimeofday getwd putenv strdup strtoul uname)

GNOME_CHECK_GUILE

ALL_LINGUAS="de es fr it ko cs no ga"
AM_GNU_GETTEXT

dnl genius
PROGRAMS_GENIUS=""
GMP_LIB=""
AC_CHECK_LIB(gmp, mpz_init_set,[
   AM_PROG_LEX
   AC_PROG_YACC
   PROGRAMS_GENIUS=genius
   GMP_LIB=-lgmp],
   [AC_CHECK_LIB(gmp2, mpz_init_set_d,[
      AM_PROG_LEX
      AC_PROG_YACC
      PROGRAMS_GENIUS=genius
      GMP_LIB=-lgmp2
      AC_DEFINE(HAVE_GMP2_INCLUDE_DIR)])])
AC_SUBST(PROGRAMS_GENIUS)
AC_SUBST(GMP_LIB)

dnl gtop
PROGRAMS_GTOP=""
AC_CHECK_HEADERS(linux/tasks.h,[PROGRAMS_GTOP=gtop])
AC_SUBST(PROGRAMS_GTOP)

dnl gsu
AC_CHECK_FUNCS(endgrent endpwent)

dnl Needed on SysV for shadow passwords.
AC_CHECK_LIB(sec, main)

dnl Needed on Linux for shadow passwords.
AC_CHECK_LIB(shadow, main)

AC_EGREP_HEADER(sp_pwdp, shadow.h,
		AC_CHECK_FUNC(getspnam, AC_DEFINE(HAVE_SHADOW_H)))

AC_CHECK_FUNCS(crypt)
if test $ac_cv_func_crypt = no; then
  # SCO-ODT-3.0 is reported to need this library for crypt.
  AC_CHECK_LIB(ufc, crypt, LIBS="$LIBS -lufc",
    [# NetBSD needs this library for crypt.
    AC_CHECK_LIB(crypt, crypt)])
fi

dnl gsu end.

dnl Check for libgtop
GNOME_INIT_LIBGTOP
dnl Check for fileutils, used in guname for diskusage stuff
GNOME_FILEUTILS_CHECKS

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

AC_OUTPUT([
Makefile
po/Makefile.in
intl/Makefile
macros/Makefile
cromagnon/Makefile
edit-menus/Makefile
find-file/Makefile
gsearchtool/Makefile
genius/Makefile
gcalc/Makefile
gedit/Makefile
ghex/Makefile
gncal/Makefile
gncal/versit/Makefile
gpostit/Makefile
gtop/Makefile
gtop/proc/Makefile
gtt/Makefile
mini-utils/Makefile
mini-utils/grun/Makefile
mini-utils/gshutdown/Makefile
mini-utils/guname/Makefile
mini-utils/gw/Makefile
mini-utils/grun-minimalist/Makefile
notepad/Makefile
gdiskfree/Makefile
intl/Makefile
stamp.h],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
